plugins {
    id 'net.researchgate.release' version '2.6.0' apply false
    id 'com.github.johnrengelman.shadow' version '5.1.0' apply false
}

subprojects { subproject ->

    // FIXME this plugin is currently not working for multi-module projects even when defined at the top level only
    // Will need to release and TAG manually for now
    subproject.apply plugin: 'net.researchgate.release'
    subproject.apply plugin: 'java-library'
    subproject.apply plugin: 'com.github.johnrengelman.shadow'
    subproject.apply plugin: 'maven-publish'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    shadowJar {
        classifier = ''
        relocate 'org.assertj', 'shadow.org.assertj'
        relocate 'org.apache.commons', 'shadow.org.apache.commons'
        relocate 'com.fasterxml.jackson', 'shadow.com.fasterxml.jackson'
    }

    dependencies {
        // Lombok
        compileOnly 'org.projectlombok:lombok:1.18.8'
        annotationProcessor 'org.projectlombok:lombok:1.18.8'
        testCompileOnly 'org.projectlombok:lombok:1.18.8'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.8'
    }

    // Adds verbose logging for TravisCI
    test {
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat "full"
        }
    }
}