plugins {
    id 'net.researchgate.release' version '2.6.0' apply false
    id 'com.github.johnrengelman.shadow' version '5.2.0' apply false
    id 'io.codearte.nexus-staging' version '0.22.0'
    id "com.diffplug.spotless" version "6.11.0" apply false
}

subprojects { subproject ->
    // FIXME this plugin is currently not working for multi-module projects even when defined at the top level only
    // Will need to release and TAG manually for now
    subproject.apply plugin: 'net.researchgate.release'
    subproject.apply plugin: 'java-library'
    subproject.apply plugin: 'com.github.johnrengelman.shadow'
    subproject.apply plugin: 'maven-publish'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    shadowJar {
        classifier = ''
        relocate 'org.assertj', 'shadow.org.assertj'
        relocate 'org.opentest4j', 'shadow.org.opentest4j'
        exclude "module-info.class"
    }

    dependencies {
        // Lombok
        compileOnly 'org.projectlombok:lombok:1.18.20'
        annotationProcessor 'org.projectlombok:lombok:1.18.20'
        testCompileOnly 'org.projectlombok:lombok:1.18.20'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

        // Logging implementation
        compileOnly 'org.slf4j:slf4j-api:2.0.0-alpha0'
    }

    // Add verbose logging for Github Actions
    test {
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat "full"
        }
    }
}
